@{
    #'std';
    using System;
    using System.IO;
    using System.Reflection;
    deployVerbosity = deployVerbosity defined ? deployVerbosity : 1;
    // cls();
    _print = deployVerbosity != 0 ? print : @{};
    //_print = @{};
    scriptDir = (new FileInfo(Assembly.GetExecutingAssembly().Location)).Directory;
    dir = scriptDir;

    if (aphidSource defined) dir = new DirectoryInfo(aphidSource);

    dir @_print('Script dir: ~Green~{0}~R~');
    libDir;
    libDirTmp;
    do
    {
        libDirTmp = Path.Combine(dir.FullName, 'Components.Aphid', 'Library');

        if (deployVerbosity > 1) {
            libDirTmp @_print('Checking dir: ~Cyan~{0}~R~');
        }

        if ('Std.alx' @Path.Combine(libDirTmp) File.Exists) {
            libDir = libDirTmp;
        }

    } while ((dir = dir.Parent) != null);

    if (libDir == null) {
        _print('~Yellow~Could not find library dir~R~');
        exit(0);
    }

    _print('Found lib dir: ~Green~{0}~R~', libDir);
    _print('[~Green~+~R~] Updating standard headers');

    Directory.GetFiles(libDir, '*.alx')->@ (src){
        srcFilename = src |> Path.GetFileName;
        dst = Path.Combine(scriptDir.FullName, 'Library', srcFilename);

        if (deployVerbosity > 1) {
            _print('Copying ~Cyan~{0}~R~ to ~Cyan~{1}~R~', src, dst);
        } else if (deployVerbosity == 1) {
            _print('Copying ~Cyan~{0}~R~', srcFilename);
        }

        File.Copy(src, dst, true);
    };

    _print();
}();