_p = macro(@{
    var x = [];
    var y;
});

_p2 = macro(@{
    var x = [];
    var y;
    var i = 0;
});

extend list { 
	count: @(l) __list.count(l),
		
	add: @(l, item) { 
		__list.add(l, item);
		ret l;
	},

	insert: @(l, index, item) { 
		__list.insert(l, index, item);
		ret l;
	},

	iter: @(l, action) { 
		for (x in l) action(x);
	},

	contains: @(l, item) __list.contains(l, item),

	select: @(l, selector) {
		_p();
		for (y in l) __list.add(x, selector(y));
		ret x;
	},

	where: @(l, predicate) { 
		_p();
		for (y in l) if (predicate(y)) __list.add(x, y);
		ret x;
	},

	first: @(l, predicate) {
        var x;
		for (x in l) if (predicate(x)) ret x;
	},

	selectMany: @(l, selector) {
		_p();
		for (y in l) for (z in selector(y)) x.add(z);
		ret x;
	},

	distinct: @(l) {
		_p();
		for (y in l) if (!__list.contains(x, y)) x.add(y);
		ret x;
	},

	any: @(l, predicate) {
		if (predicate defined) {
            var x;
			for (x in l) if (predicate(x)) ret true;
			ret false;
		} else {
			ret __list.count(l) != 0;
		}
	},

	all: @(l, predicate) {
        var x;
		for (x in l) if (!predicate(x)) ret false;
		ret true;
	},

	concat: @(l, second) {
		_p();
		for (y in l) x.add(y);
		for (y in second) x.add(y);
		ret x;
	},

	skip: @(l, count) {
		_p2();
		for (y in l) {
			if (i >= count) x.add(y);
			i++;
		}
		ret x;
	},

	take: @(l, count) {
		_p2();
		for (y in l) {
			if (i < count) x.add(y);
			i++;
		}
		ret x;
	},

	aggregate: @(l, acc) {
		if (l.count() == 1) ret l[0];
		else {
            var x;
			var s = l[0];
			for (x in l.skip(1)) s = acc(s, x);
			ret s;
		}
	},

	orderBy: @(l, keySelector) __list.orderBy(l, keySelector),
		
	orderByDescending: @(l, keySelector) __list.orderByDescending(l, keySelector),
		
	addAll: @(l) l.aggregate(@(x, y)x + y),

	clone: @(l) {
		_p();
		l.iter(x.add);
		ret x;
	},
}