var exports;
var resources;
var csharp;

@{
    #'CodeDom';
    #'Std';
    #'Meta';

    using System.Security.Cryptography;

    var md5Provider = new MD5CryptoServiceProvider();
    
    var md5 = {
        hash: md5Provider.ComputeHash,
        hex: @(buffer) buffer |> hash -> @format('{0:x2}') +> @(x, y) x + y
    };

    exports = {
        compileFile: @(namespace, srcFile){
            printInfo('Parsing ~Cyan~{0}~R~', srcFile);
            ret srcFile |> File.ReadAllText @compile(namespace);
        },

        compile: @(namespace, srcCode){
            var srcAst;
    
            try {
                srcAst = srcCode |> parse;
            } catch (e) {
                AphidCli.DumpException(e.exception.InnerException, srcCode);
                ret null;
            }

            var bytecode = AphidByteCode.Encode(srcAst);
            var resource = 'ByteCode_{0}' :: (bytecode |> md5.hex);

            var cs =
                srcAst
                |> exports.find
                -> (@() exports.generate(namespace, resource, $_))
                @join('\r\n');

            cs =
                '\r\n' +
                cs + '\r\n' +
                resources.embed(namespace, resource, bytecode);

            [ 'Components.Aphid.Interpreter', 'Components.Aphid.Parser' ]->@()
                cs = 'using global::' + $_ + ';\r\n' + cs.Replace($_ + '.', '');

            ret cs.Trim() != '' ? cs : null;
        },

        resolve: @(type) this.{'$aphid'}.InteropTypeResolver.ResolveType(
            this.{'$imports'}.ToArray(),
            [ type ],
            true,
            true),

        find: @(srcAst) {
            using System.Collections.Generic;
            var exports = new List[AphidObject]();

            srcAst @ast.walk(@(node, context) {
                if (!context.isRoot && !context.isBlock && node.isId() && node.firstAttr(export)) {
                    var exportExp = context.stack
                        ->@.node
                        @aq.first(@(x)
                            x.GetType() == BinaryOperatorExpression &&
                            x.isOp(AphidTokenType.AssignmentOperator));

                    var retType =
                        ($(node.attrs() #!) 1: 'void', node.attr(1).Identifier)
                        |> resolve;

                    var memberPath = exportExp.lhs().GetChildren()->@.Identifier;
                    var className = memberPath[0];
                    var propertyName = memberPath[1];
                
                    var params = exportExp.rhs().Args
                        ->@()({
                            type: ($_.noAttrs() ? 'AphidObject' : $_.attr(0).Identifier) |> resolve,
                            name: $_.Identifier
                        });

                    {
                        retType,
                        className,
                        propertyName,
                        params,
                        func: exportExp.rhs()
                    } |> exports.Add;
                }
            });

            ret exports;
        },

        generate: @(namespace, resource, export) {
        
            var typeName = export.className;
            var delName = export.propertyName + 'Delegate';
            var fieldName = '_' + export.propertyName;
            var f = code.field(code.typeExp(typeName), fieldName);
            
            var d = code
                .namespace(namespace)
                .type(typeName)
                .partial()
                .field(delName, fieldName)
                .static()
                .next()
                .withPublic()
                .property(delName, export.propertyName)
                .static()
                .getStmt(
                    f.ifNull([
                        code
                            .ctor(AphidInterpreter, [])
                            .call(
                                'Interpret',
                                [
                                    code.typeExp(AphidByteCode).call(
                                        'Decode',
                                        [ code.var('Resources').field(resource) ])
                                ])
                            .stmt(),
                    ]))
                .wrapField(f)
                .next()
                .delegate(export.retType, delName);

            export.params->@() d.param($_.type, $_.name);

            ret d.next().namespace().code();
        }
    };

    resources = {
        embed: @(namespace, resource, buffer) {
            var typeName = 'Resources';
            var type = byte.MakeArrayType();
            var fieldName = '_' + resource;

            ret code
                .namespace(namespace)
                .type(typeName)
                .partial()
                .field(type, fieldName, code.arrayCreate(type, buffer->code.primitive))
                .static()
                .next()
                .withPublic()
                .property(type, resource)
                .static()
                .getField(code.typeExp(typeName).field(fieldName))
                .next()
                .namespace()
                .code();
        },
    };

    emit = macro(@(text) {
        sb.Append(getTab());
        sb.Append(text);
    });
    
    emitLine = macro(@(text) {
        sb.Append(getTab());
        sb.AppendLine(text);
    });

    emitFormat = macro(@(fmt, fmtArg) {
        sb.Append(getTab());
        sb.AppendFormat(fmt, fmtArg);
    });

    emitLineFormat = macro(@(fmt, fmtArg) {
        sb.Append(getTab());
        sb.AppendFormat(fmt, fmtArg);
        sb.AppendLine();
    });

    csharp = {
       fromAst: @(block) {
           using System.Text;

            var sb = new StringBuilder(0x1000000);
            var indentCount = 0;
            var tabList = 0..128->@(i) i * '    ';
            getTab = macro(@{ tabList[indentCount] });
            indent = macro(@{ indentCount++ });
            unindent = macro(@{ indentCount-- });

            var compile = @(block, compileDelim, genericParam) {
                if (genericParam defined) {
                    emitLineFormat('new List<{0}>', genericParam);
                } else {
                    emitLine('new List<AphidExpression>');
                }

                emitLine('{');
                indent();
                block->@() compileNode($_, ',');
                unindent();

                if (compileDelim defined && compileDelim.Length != 0) {
                    emitLine('},');
                } else {
                    emitLine('}');
                }
            };

            var compileNode = @(node, compileNodeDelim) {
                var t = node.GetType();
                emitLineFormat('new {0}(', t.Name);
                indent();
                
                var props = t.GetProperties();
                var ctorParams = props-?@() $_.DeclaringType == t && $_.PropertyType != AphidExpressionType;
                var i = 0;
                ctorParams->@()$_.GetValue(node) @compileArg(i++, i == (ctorParams #!));
                
                unindent();
                if (compileNodeDelim defined) {
                    emitLine('),');
                } else {
                    emitLine(')');
                }
                
            };

            var compileArg = @(index, isLast, arg) {
                if (arg == null) {
                    if (isLast) {
                        emitLine('null');
                        ret null;
                    } else {
                        emitLine('null,');
                        ret null;
                    }
                }

                var t = arg.GetType();

                if (t == AphidTokenType) {
                    if (isLast) {
                        emitLineFormat('AphidTokenType.{0}', arg);
                    } else {
                        emitLineFormat('AphidTokenType.{0},', arg);
                    }
                } else if (t.BaseType == AphidExpression) {
                    if (isLast) {
                        compileNode(arg);
                    } else {
                        compileNode(arg, ',');
                    }
                // Todo: investigate optimizing via hardcoding cases for generic 
                // types e.g. List[IdentifierExpression]
                // } else if (IEnumerable[IdentifierExpression].IsAssignableFrom(t)) {
                //     compile(arg, delim, 'IdentifierExpression');
                } else if (IEnumerable[AphidExpression].IsAssignableFrom(t)) {
                    compile(arg, isLast ? '' : ',', t.GetGenericArguments()[0].Name);
                } else if (t == string) {
                    if (isLast) {
                        emitLineFormat('"{0}"', arg.Replace('"', '\\"'));
                    } else {
                        emitLineFormat('"{0}",', arg.Replace('"', '\\"'));
                    }
                } else if (t == decimal) {
                    if (isLast) {
                        emitLine(arg.ToString());
                    } else {
                        emitLineFormat('{0},', arg);
                    }
                } else if (t == bool) {
                    if (isLast) {
                        emitLine(arg.ToString().ToLower());
                    } else {
                        emitLineFormat('{0},', arg.ToString().ToLower());
                    }
                } else {
                    throw 'Not implemented: {0}' :: t;
                }
            };

            emitLine('namespace Test');
            emitLine('{');
            indent();
            emitLine('using Components.Aphid.Lexer;');
            emitLine('using Components.Aphid.Parser;');
            emitLine('using System.Collections.Generic;');
            emitLine('');
            emitLine('public static class TestClass');
            emitLine('{');
            indent();
            emitLine('public static List<AphidExpression> GetAst()');
            emitLine('{');
            indent();
            emit('return ');
            compile(test);
            sb.AppendLine(';');
            unindent();
            emitLine('}');
            unindent();
            emitLine('}');
            unindent();
            emitLine('}');

            ret sb;
       } 
    };
}();