_qp = macro(@{
    var x = [];
    var y;
});

_qp2 = macro(@{
    _qp();
    var z;
});

_qp3 = macro(@{
    _qp();
    var i = 0;
});

var aphidQuery = {
    where: @(predicate, list) {
        _qp();
        for (y in list) if (predicate(y)) x.add(y);
        ret x;
    },

    select: @(selector, list) {
        _qp();
        for (y in list) y |> selector |> x.add;
        ret x;
    },

    selectMany: @(selector, list) {
        _qp2();
        for (y in list) for (z in selector(y)) x.add(z);
        ret x;
    },

    any: @(predicate, list) {
        var x;
        for (x in list) if (predicate(x)) ret true;
        ret false;
    },

    first: @(predicate, list) {
        if ($args.Count == 2) {
            var x;
            for (x in list) if (predicate(x)) ret x;
        } else {
            var x;
            for (x in predicate) ret x;
        }
    },

    last: @(predicate, list) {
        var x;
        var last = null;

        if ($args.Count == 2) {
            for (x in list) if (predicate(x)) last = x;
        } else {
            for (x in predicate) last = x;
        }

        ret last;
    },

    distinct: @(list) {
        _qp();
        for (y in list) if (!__list.contains(x, y)) x.add(y);
        ret x;
    },

    iter: @(action, list) {
        var x;
        for (x in list) action(x);
    },

    all: @(predicate, list) {
        var x;
        for (x in list) if (!predicate(x)) ret false;
        ret true;
    },

    count: @(list) __list.count(list),

    concat: @(list, otherList) {
        _qp();
        for (y in otherList) x.add(y);
        for (y in list) x.add(y);
        ret x;
    },

    skip: @(count, list) {
        _qp3();

        for (y in list) {
            if (i >= count) x.add(y);
            i++;
        }

        ret x;
    },

    take: @(count, list) {
        _qp3();

        for (y in list) {
            if (i < count) x.add(y);
            i++;
        }

        ret x;
    },

    takeWhile: @(predicate, list) {
        var x = [];

        list for {
            if (predicate($_)) {
                x.add($_);
            } else {
                break;
            }
        };

        ret x;
    },
    
    aggr: @(acc, list) {
        if (list.count() == 1) ret list[0];
        else {
            var x;
            var s = list[0];
            for (x in list.skip(1)) s = acc(s, x);

            ret s;
        }
    },

    add: @(list) aq.aggr(@(x, y)x + y, list),
};

var aq = aphidQuery;