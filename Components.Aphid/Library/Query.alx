_p = macro(@{
    x;
    y;
    x = [];
});

_p2 = macro(@{
    _p();
    z;
});

_p3 = macro(@{
    _p();
    i;
    i = 0;
});

aphidQuery = {
    where: @(predicate, list) {
        _p();
        for (y in list) if (predicate(y)) x.add(y);
        ret x;
    },

    select: @(selector, list) {
        _p();
        for (y in list) y |> selector |> x.add;
        ret x;
    },

    selectMany: @(selector, list) {
        _p2();
        for (y in list) for (z in selector(y)) x.add(z);
        ret x;
    },

    any: @(predicate, list) {
        x;
        for (x in list) if (predicate(x)) ret true;
        ret false;
    },

    first: @(predicate, list) {
        if ($args.Count == 2) {
            x;
            for (x in list) if (predicate(x)) ret x;
        } else {
            x;
            for (x in predicate) ret x;
        }
    },

    last: @(predicate, list) {
        x;
        last;
        last = null;

        if ($args.Count == 2) {
            for (x in list) if (predicate(x)) last = x;
        } else {
            for (x in predicate) last = x;
        }

        ret last;
    },

    distinct: @(list) {
        _p();
        for (y in list) if (!__list.contains(x, y)) x.add(y);
        ret x;
    },

    iter: @(action, list) {
        x;
        for (x in list) action(x);
    },

    all: @(predicate, list) {
        x;
        for (x in list) if (!predicate(x)) ret false;
        ret true;
    },

    count: @(list) __list.count(list),

    concat: @(list, otherList) {
        _p();
        for (y in otherList) x.add(y);
        for (y in list) x.add(y);
        ret x;
    },

    skip: @(count, list) {
        _p3();

        for (y in list) {
            if (i >= count) x.add(y);
            i++;
        }

        ret x;
    },

    take: @(count, list) {
        _p3();

        for (y in list) {
            if (i < count) x.add(y);
            i++;
        }

        ret x;
    },

    takeWhile: @(predicate, list) {
        x;
        x = [];

        list for {
            if (predicate($_)) {
                x.add($_);
            } else {
                break;
            }
        };

        ret x;
    },
    
    aggr: @(acc, list) {
        if (list.count() == 1) ret list[0];
        else {
            s;
            x;
            s = list[0];
            for (x in list.skip(1)) s = acc(s, x);
            ret s;
        }
    },

    add: @(list) aq.aggr(@(x, y)x + y, list),
};

aq = aphidQuery;